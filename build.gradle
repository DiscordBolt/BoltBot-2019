buildscript {
    ext {
        boltBotVersion = '3.4-SNAPSHOT'

        springBootVersion = '2.1.1.RELEASE'
        discord4jVersion = 'v3-SNAPSHOT'
        commandVersion = 'master-SNAPSHOT'
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'com.google.cloud.tools.jib' version '0.9.12'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'com.discordbolt'
version "$boltBotVersion"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

bootJar {
    baseName = 'boltbot'
    version = "$boltBotVersion"
}

repositories {
    mavenCentral()
    // Discord 4J & Command API
    maven { url 'https://jitpack.io' }
    // Reactor Spring Fix TODO remove this. Should no longer be needed
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    // Spring Data
    maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
}

dependencies {
    // Spring Boot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "$springBootVersion"
    // Spring Data
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: "$springBootVersion"
    // Spring MongoDB Reactive
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb-reactive', version: "$springBootVersion"
    // Discord4J
    implementation group: 'com.discord4j.discord4j', name: 'discord4j-core', version: "$discord4jVersion"
    // Command API
    implementation group: 'com.github.DiscordBolt', name: 'CommandAPI', version: "$commandVersion"
    // Reflections (For BotModules)
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "$springBootVersion"
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
    testCompile group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo'
}

task defineProperties(dependsOn: processResources) {
    doLast {
        file("$buildDir/resources/main/application.properties").withWriter { w ->
            Properties p = new Properties()
            p.load(new FileInputStream(new File('src/main/resources/application.properties')))
            p['boltbot.version'] = project.version.toString()
            p['discord.token'] = System.getenv('DISCORD_TOKEN')
            p['twitch.clientid'] = System.getenv('TWITCH_CLIENT_ID')
            p.store w, null
        }
    }
}

def getCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

classes {
    dependsOn defineProperties
}

jib {
    from {
        image = 'openjdk:10-jre'
    }
    to {
        if (project.hasProperty('DockerTag')) {
            image = "techtony96/boltbot:" + project.findProperty("DockerTag")
        } else {
            image = "techtony96/boltbot:$boltBotVersion-" + getCommitHash()
        }
        auth {
            username = 'techtony96'
            password = project.findProperty('DockerPassword')
        }
    }
    container {
        jvmFlags = ['-Xms512m', '-Xmx512m']
        mainClass = 'com.discordbolt.boltbot.BoltBot'
        args = ['--spring.config.location=/app/resources/application.properties']
    }
}

wrapper {
    gradleVersion = '4.9'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
