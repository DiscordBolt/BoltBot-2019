group 'DiscordBolt'
version '3.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 10

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.discordbolt.boltbot.BoltBot'
    }
}

shadowJar {
    destinationDir = file('artifacts')
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        // Used for DiscordBolt dependencies and dev versions of Discord4J
        url  'https://jitpack.io'
    }
}

dependencies {
    // Discord 4J
    implementation 'com.github.Discord4J.Discord4J:Discord4J-core:v3-SNAPSHOT'
    // Logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    // Command API
    //implementation group: 'com.github.DiscordBolt', name:'CommandAPI', version: 'v2-SNAPSHOT'
    // Hibernate
    implementation group:'org.hibernate', name:'hibernate-core', version: '5.2.17.Final'
    // Throwing Lambdas
    implementation group: 'com.github.fge', name: 'throwing-lambdas', version: '0.5.0'

    // JUnit
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    // Mockcord
    //testImplementation group: 'com.github.DiscordBolt', name:'Mockcord', version:'master-SNAPSHOT'
}

task defineVersion(dependsOn: processResources) {
    doLast {
        file("$buildDir/resources/main/BoltBot.properties").withWriter { w ->
            Properties p = new Properties()
            p.load(new FileInputStream(new File('src/main/resources/BoltBot.properties')))
            p['VERSION'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn defineVersion
}
