group 'com.discordbolt'
version '3.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = "2.0.2.RELEASE"

        discord4jVersion = "v3-SNAPSHOT"
        storeVersion = "master-SNAPSHOT"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'boltbot'
    version =  '3.0-SNAPSHOT'
}

sourceCompatibility = 10
targetCompatibility = 10


repositories {
    mavenCentral()
    maven {
        // Discord 4J & Command API
        url  "https://jitpack.io"
    }
    maven {
        // Spring Data
        url "https://repository.jboss.org/nexus/content/repositories/releases"
    }
}

dependencies {
    // Spring Boot
    implementation group:'org.springframework.boot', name:'spring-boot-starter-web'
    // Spring Data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java'
    implementation 'javax.xml.bind:jaxb-api:2.2.11'
    // Discord4J
    implementation "com.discord4j.discord4j:discord4j-core:$discord4jVersion"
    implementation "com.github.discord4j.stores:stores-jdk:$storeVersion"
    // Reflections (For BotModules)
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name:'spring-boot-starter-test'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

task defineVersion(dependsOn: processResources) {
    doLast {
        file("$buildDir/resources/main/application.properties").withWriter { w ->
            Properties p = new Properties()
            p.load(new FileInputStream(new File('src/main/resources/application.properties')))
            p['boltbot.version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn defineVersion
}

